title: 경제 정책
new-page: true
is-card: true
tags: govt_affairs, cabinet
on-arrival: economic_policy_timer += 12; month_actions += 1; nationalize_budget = 4; nationalize_budget -= 1 if works_councils >= 2; nationalize_budget -= 1 if works_councils >= 3; nationalize_budget -= 1 if socializations + nationalization_progress >= 2; wtb_budget = 4; wtb_budget -= 1 if works_councils >= 2 or socializations >= 1 or pro_labor >= 2
view-if: spd_in_government = 1 and (economic_minister_party == "SPD" or finance_minister_party == "SPD") and economic_policy_timer <= 0 and economic_plan > 0
card-image: img/portraits/SchmidtRobert.jpg
go-to: economic_plan_menu if not lautenbach_adopted; economic_plan_dilemma if lautenbach_adopted
#subtitle: Policies to address the economic crisis.

= 경제 정책

우리는 이미 경제 계획을 수립했습니다. 이제 이를 실행에 옮겨야 합니다. [? if budget < 0: 예산 적자가 너무 심해지면, 자본 파업이 일어날 것이 거의 확실합니다. ?]

@economic_plan_dilemma

[+ public_works_name +] 계획은 우리 이전에 브뤼닝 내각에 의해 이미 실행되었습니다. 이 계획을 유지하며 잠재적으로 범위를 확장해야 할까요, 아니면 우리만의 경제 전략을 추구해야 할까요?

- @lautenbach_continuation
- @lautenbach_wtb
- @nationalize_1
- @nationalize_2
- @cancel_advisor_action

@lautenbach_continuation
title: [+ public_works_name +] 계획을 계속해서 실행한다.
choose-if: unemployed >= 10
unavailable-subtitle: [? if unemployed < 10: 실업률이 낮아 일자리 창출이 필요하지 않습니다. ?] [? if inflation >= 5: 인플레이션의 위협으로 인해 제국 은행이 계획 실행을 중단시켰습니다. ?] 
on-arrival: lautenbach_plan_time = time + 12 if time >= lautenbach_plan_time; lautenbach_plan_time += 12 if lautenbach_plan_time >= time; works_program += 1; workers_spd += 3*(1-dissent); unemployed_spd += 4*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 2*(1-dissent); rural_spd += 2*(1-dissent); workers_spd += 3*(1-dissent) if lautenbach_wtb; unemployed_spd += 4*(1-dissent) if lautenbach_wtb; new_middle_spd += 3*(1-dissent) if lautenbach_wtb; old_middle_spd += 2*(1-dissent) if lautenbach_wtb; rural_spd += 2*(1-dissent) if lautenbach_wtb; workers_nsdap -= 4 if lautenbach_wtb; workers_nsdap -= 2; z_relation += 5; ddp_relation += 5; dvp_relation += 5; lvp_relation += 5; coalition_dissent -= 1 if coalition_dissent >= 1; capital_strike_progress -= 1 if capital_strike_progress >= 0; dvp_left += 2; ddp_left += 4; ddp_cohesion += 2; lvp_left += 3; pro_republic += 3; economic_plan_goal_spd += 1; unemployed -= 2 if unemployed >= 25; unemployed -= 1; inflation += 1; economic_growth += 1.6; economy_goal_spd_peoples += 1; kpd_relation -= 8 if in_popular_front or in_left_front; kpd_coalition_dissent += 1

공공 사업에 대한 지출 덕분에 실업 보험과 복지 비용이 감소했습니다. 동시에 새로운 자금이 경제에 순환하면서 승수 효과를 통해 세수가 증가하여, 지출의 일부를 회수하는 데 도움이 되었습니다. 우리는 이 새로운 수입의 대부분을 프로그램에 계속 재투자하고 있습니다.

경제가 마침내 회복의 조짐을 보이고 있습니다. 부르주아 정당들은 우리가 그들의 경제 회복 계획을 충실히 이행하는 것을 보고 기뻐하며 전폭적인 지지를 보내고 있습니다. 우리의 성공으로 형성된 투자자들의 신뢰는 외국인 투자자들의 자본 유입과 함께 민간 부문의 소규모 부활로 이어졌습니다. [? if not lautenbach_wtb: 하지만 노동자들은 임금 인상 속도가 느리다며 불만을 표하고 있습니다. ?]

[? if in_popular_front or in_left_front: <span style="color: #700000;">**KPD**</span>는 이 경제 계획에 반대하며, 이를 진정한 <span style="color: #9b0000;">사회주의</span>를 향한 단계가 아닌 자본주의를 구제하는 행위로 간주합니다. ?]

@lautenbach_wtb
title: [+ public_works_name +] 계획에 WTB 계획을 접목하여, 공공 사업과 임금을 대폭 확대한다.
subtitle: 예산 -2.
unavailable-subtitle: 예산 -2. [? if budget < 2 : 필요한 예산이 부족합니다. ?] [? if unemployed < 8 : 실업률이 낮아 일자리 창출이 필요하지 않습니다. ?] [? if inflation >= 5: 인플레이션의 위협으로 인해 라이히스뱅크가 계획 실행을 중단시켰습니다. ?] 
view-if: wtb_adopted
choose-if: unemployed >= 10 and budget >= 2 and inflation < 5
on-arrival: lautenbach_wtb = 1; lautenbach_plan_time = time + 12 if time >= lautenbach_plan_time; lautenbach_plan_time += 12 if lautenbach_plan_time >= time; works_program += 1; workers_spd += 6*(1-dissent); unemployed_spd += 8*(1-dissent); new_middle_spd += 4*(1-dissent); old_middle_spd += 4*(1-dissent); rural_spd += 4*(1-dissent); workers_nsdap -= 6; z_relation += 3; ddp_relation += 3; dvp_relation += 3; lvp_relation += 3; coalition_dissent -= 1 if coalition_dissent >= 1; capital_strike_progress -= 1 if capital_strike_progress >= 3; dvp_left += 2; ddp_left += 3; ddp_cohesion += 1; lvp_left += 2; pro_republic += 5; economic_plan_goal_spd += 1; unemployed -= 2 if unemployed >= 15; unemployed -= 2; inflation += 2; economic_growth += 2.6; economy_goal_spd_peoples += 1; kpd_relation -= 8 if in_popular_front or in_left_front; kpd_coalition_dissent += 1
max-visits: 1

우리는 공공 사업 프로그램을 상당히 확장했고, 임금 인상 캠페인과 맞물려 경제에 자금이 돌기 시작했습니다. 부르주아 정당들은 인플레이션과 지출에 우려를 표하며 더 신중한 접근을 선호합니다. 하지만 그들은 우리가 쌓아 올린 흑자를 보며 안심하고 우리의 계획을 받아들입니다.

공공 사업에 대한 지출 덕분에 실업 보험과 복지 비용이 감소했습니다. 동시에 새로운 자금이 경제에 순환하면서 승수 효과를 통해 세수가 증가하여, 지출의 일부를 회수하는 데 도움이 되었습니다. 우리는 이 새로운 수입의 대부분을 프로그램에 계속 재투자하고 있습니다.

[? if in_popular_front or in_left_front: <span style="color: #700000;">**KPD**</span>는 이 경제 계획에 반대하며, 이를 진정한 <span style="color: #9b0000;">사회주의</span>를 향한 단계가 아닌 자본주의를 구제하는 행위로 간주합니다. ?]

@economic_plan_menu

- @wtb_2
- @wtb_2_deficit
- @wtb_continuation
- @implement_wtb_no_deficit
- @implement_wtb_deficit
- @nationalize_1
- @nationalize_2
- @moderate_plan
- @moderate_plan_limited_budget
- @austerity
- @root: 경제 계획을 실행하지 않는다.
- @easy_discard
- @cancel_advisor_action

@austerity
title: 잠재적인 긴축 조치를 검토한다.
subtitle: 어떻게든 자금을 마련해야 합니다.
view-if: black_thursday_seen

- @cut_salaries
- @industrial_levy
- @economic_plan_menu: 뒤로 가기.

@cut_salaries
title: 공무원 급여를 삭감한다.
subtitle: 이는 중산층을 급진화시킬 것입니다. 예산 +1
unavailable-subtitle: 이미 임금을 삭감했습니다.
choose-if: not government_salaries_cut
on-arrival: government_salaries_cut = 1; new_middle_spd -= 8; new_middle_ddp -= 4; new_middle_dvp -= 4; new_middle_lvp -= 8; new_middle_nsdap += 12; pro_republic -= 5; labor_dissent += 15; left_dissent += 15; center_dissent += 10; unemployed += 1; inflation -= 0.5; economic_growth -= 0.4; budget += 1; ddp_right += 3; dvp_right += 6; lvp_right += 5; ddp_cohesion -= 3; ddp_relation -= 10; dvp_relation -= 15; z_relation -= 6; lvp_relation -= 12; goal_spd_cancel_peoples += 1; goal_spd_cancel += 1
go-to: austerity

우리는 독일 경제 회복을 위한 고통스럽지만 필요한 조치라는 명분으로 공무원 임금을 약 15% 삭감했습니다. 하지만 이러한 정당화는 많은 사람을 설득하지 못했고, 다수가 민주주의 정당에서 등을 돌려 급진적인 <span style="color: #7A3C00;">**NSDAP**</span>로 지지를 옮기고 있습니다.

@industrial_levy
title: 산업 부담금을 유지한다.
subtitle: 영 안으로 인해 이제는 불필요해진 배상금 관련 세금입니다. 기업들이 불쾌해할 것입니다. 예산 +1
unavailable-subtitle: 이미 세금을 유지했습니다.
view-if: young_plan_ratified
choose-if: not industrial_levy
on-arrival: industrial_levy = 1; reformist_dissent += 10; left_strength += 5; left_dissent -= 5; new_middle_spd -= 4; new_middle_ddp -= 2; new_middle_nsdap += 6; new_middle_spd -= 4; old_middle_ddp -= 2; old_middle_nsdap += 6; pro_republic -= 3; capital_strike_progress += 3; unemployed += 1; inflation -= 0.8; economic_growth -= 0.8; upper_tax_rates += 1; budget += 1; ddp_right += 3; dvp_right += 8; lvp_right += 6; ddp_cohesion -= 3; ddp_relation -= 8; dvp_relation -= 15; z_relation -= 5; lvp_relation -= 12; hindenburg_angry += 20; goal_spd_cancel_peoples += 1
go-to: austerity

우리는 산업 부담금이 더 이상 배상금 지불에 사용되지 않음에도 불구하고 징수를 유지했습니다. 예상대로 재계의 분노를 샀습니다[? if president == "Hindenburg":, 그들은 힌덴부르크 대통령에게 불만을 토로할 것입니다?]. 우리의 결정에 대한 반발로, 자본가들은 <span style="color: #7A3C00;">극</span><span style="color: #3E88B3;">우</span>파에 대한 재정 지원을 두 배로 늘릴 것입니다[? if in_grand_coalition and not lvp_formed:, 그리고 우리 정부가 무너지길 바라며 <span style="color: #C0A054;">**DVP**</span>에게도 지원할 것입니다?][? if (in_weimar_coalition or in_grand_coalition) and lvp_formed:, 그리고 우리 정부가 무너지길 바라며 <span style="color: #FFCC00;">**LVP**</span>에게도 지원할 것입니다?].

@wtb_2
title: WTB 계획을 계속해서 실행한다.
subtitle: 예산 -3
unavailable-subtitle: 실업률이 낮아 WTB 계획을 시행할 시기가 지났습니다.
view-if: wtb_implemented > 2 and budget >= 3 
choose-if: unemployed > 8
on-arrival: wtb_enactment_2_finish = time + 10; works_program += 1; budget -= 3; workers_spd += 5*(1-dissent); workers += 1 if works_program <= 4; unemployed_spd += 7*(1-dissent); workers_spd -= 2*(1-dissent) if schleicher_spd; unemployed_spd -= 3*(1-dissent) if schleicher_spd; reichswehr_strength += 35 if schleicher_spd; reichswehr_loyalty += 0.1 if reichswehr_spd; reichswehr_militancy += 0.5 if reichswehr_spd; workers_nsdap -= 3; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; inflation -= 1 if schleicher_spd; dvp_left += 1; ddp_left += 3; ddp_cohesion += 1; lvp_left += 2; pro_republic += 3; economic_plan_goal_spd += 1; wtb_rally -= 1

[? if works_program == 4 : 우리는 WTB 계획의 2단계를 시작했습니다. ?][? if works_program > 4 :  우리는 WTB 계획을 계속해서 실행했습니다. ?] 공공 사업에 더해, 이제는 산업 재건에 집중하고 있습니다.


@wtb_2_deficit
title: WTB 계획을 계속해서 실행한다.
subtitle: 예산 -3 - 재정 적자가 발생할 것입니다.
unavailable-subtitle: 실업률이 낮아 적자 지출을 감행할 필요가 없습니다.
view-if: wtb_implemented > 2 and budget < 3 and budget >= -3
choose-if: unemployed > 10
on-arrival: works_program += 1; budget -= 3; workers_spd += 4*(1-dissent); unemployed_spd += 6*(1-dissent); workers += 1 if works_program <= 4; workers_spd -= 2*(1-dissent) if schleicher_spd; unemployed_spd -= 3*(1-dissent) if schleicher_spd; reichswehr_strength += 35 if schleicher_spd; reichswehr_loyalty += 0.1 if reichswehr_spd; reichswehr_militancy += 0.5 if reichswehr_spd; workers_nsdap -= 2; coalition_dissent += 1; coalition_dissent -= 1 if z_leader == "Kaiser"; capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; inflation -= 1 if schleicher_spd; dvp_right += 1; ddp_right += 1; ddp_cohesion -= 1; lvp_right += 1; economic_plan_goal_spd += 1; wtb_rally -= 1

지속적인 적자 지출에도 불구하고 우리는 WTB 계획을 계속해서 실행했습니다. 공공 사업에 더해, 이제는 산업 재건에 집중하고 있습니다.


@wtb_continuation
title: 성과를 되돌아보고, 실행을 계속한다...
view-if: wtb_implemented >= 1 and wtb_implemented <= 2
on-arrival: wtb_enactment_continuation_finish = time + 6 if wtb_implemented == 1; wtb_enactment_continuation_finish = wtb_enactment_continuation_finish + 6 if wtb_implemented == 2 and wtb_enactment_continuation_finish > time; wtb_enactment_continuation_finish = time + 6 if wtb_implemented == 2 and wtb_enactment_continuation_finish < time; budget += 1 if works_program <= 2; workers_spd += 5*(1-dissent); workers += 2 if works_program <= 2 else workers += 1; unemployed_spd += 8*(1-dissent); workers_spd -= 2.5*(1-dissent) if schleicher_spd; unemployed_spd -= 4*(1-dissent) if schleicher_spd; reichswehr_strength += 20 if schleicher_spd; reichswehr_loyalty += 0.05 if reichswehr_spd; reichswehr_militancy += 0.25 if reichswehr_spd; workers_nsdap -= 2; works_program += 1; z_relation += 3; dvp_relation += 3; lvp_relation += 3; coalition_dissent -= 1 if coalition_dissent >= 1; wtb_implemented += 1; capital_strike_progress -= 1 if capital_strike_progress >= 3; dvp_left += 1; ddp_relation += 5; ddp_left += 3; ddp_cohesion += 2; lvp_left += 2; pro_republic += 5; economic_plan_goal_spd += 1; wtb_rally -= 1

공공 사업에 대한 지출 덕분에 실업 보험과 복지 비용이 감소했습니다. 새로운 자금이 경제에 순환하면서 승수 효과를 통해 세수가 증가하여, 지출의 일부를 회수했습니다. 우리는 새로운 수입의 대부분을 프로그램에 계속 재투자하고 있습니다.

마침내 경제가 호전되고 있습니다. 부르주아 정당들조차 희망을 보기 시작했습니다.

[? if in_popular_front or in_left_front: 하지만 <span style="color: #700000;">**KPD**</span>는 여전히 자본주의를 "구제"하는 우리의 정책에 반대하고 있습니다. ?]

@implement_wtb_no_deficit
title: WTB 계획을 실행한다.
subtitle: 예산 -[+ wtb_budget +]. [? if wtb_budget == 3 : 우리의 친노동 정책 덕분에 계획 비용이 절감되었습니다. ?]
unavailable-subtitle: [? if unemployed < 8: 실업률이 낮아 WTB 계획을 시행할 시기가 지났습니다.?] [? if in_grand_coalition and (finance_minister_party == "DVP" or economic_minister_party == "DVP" and dvp_ideology != "Left") and not lvp_formed: <span style="color: #C0A054;">**DVP**</span>는 [? if finance_minister_party == "DVP": 재무부?][? if economic_minister_party == "DVP" and finance_minister_party == "DVP":&nbsp;및?][? if economic_minister_party == "DVP": 경제부?] 내의 지위를 이용해 우리를 방해할 것입니다. ?][? if (in_weimar_coalition or in_grand_coalition) and (finance_minister_party == "LVP" or economic_minister_party == "LVP" and lvp_ideology != "Left") and lvp_formed: <span style="color: #FFCC00;">**LVP**</span>는 [? if finance_minister_party == "LVP": 재무부?][? if economic_minister_party == "LVP" and finance_minister_party == "LVP":&nbsp;및?][? if economic_minister_party == "LVP": 경제부?] 내의 지위를 이용해 우리를 방해할 것입니다. ?] [? if in_grand_coalition and dvp_ideology == "Right" and not lvp_formed: <span style="color: #C0A054;">**DVP**</span>는 선제적으로 불신임 투표를 소집할 것입니다. ?][? if in_weimar_coalition or in_grand_coalition and lvp_ideology == "Right" and lvp_formed: <span style="color: #FFCC00;">**LVP**</span>는 선제적으로 불신임 투표를 소집할 것입니다. ?]
view-if: budget >= wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8 and ((((economic_minister_party != "DVP" and finance_minister_party != "DVP") or dvp_ideology == "Left") and (dvp_ideology != "Right" and in_grand_coalition) or not in_grand_coalition) and not lvp_formed) or ((((economic_minister_party != "LVP" and finance_minister_party != "LVP") or lvp_ideology == "Left") and (lvp_ideology != "Right" and (in_grand_coalition or in_weimar_coalition)) or (not in_grand_coalition and not in_weimar_coalition)) and lvp_formed)
on-arrival: budget -= wtb_budget; coalition_dissent += 1 if (in_grand_coalition or in_minority_government); works_program += 1; unemployed -= 4; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); workers_spd -= 4*(1-dissent) if schleicher_spd; unemployed_spd -= 4*(1-dissent) if schleicher_spd; reichswehr_strength += 20 if schleicher_spd; reichswehr_loyalty += 0.05 if reichswehr_spd; reichswehr_militancy += 0.5 if reichswehr_spd; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 2; dvp_right += 1; ddp_left += 1; ddp_cohesion -= 1; lvp_right += 1; pro_republic += 3*(1-dissent); economic_growth += 2.8; economic_growth -= 0.8 if schleicher_spd; economic_plan_goal_spd += 1; wtb_rally = 0
achievement: wirtschaftspolitik

우리는 WTB 계획을 제정하기 시작했습니다. 대규모 공공 사업 투자를 통해 경제에 자금이 돌기 시작했습니다. 부르주아 정당들은 인플레이션과 지출에 우려를 표하며 경제 위기 해결책으로 감세를 선호합니다. 하지만 그들은 우리가 쌓아 올린 흑자를 보며 마지못해 우리의 계획을 받아들입니다.

[? if in_popular_front or in_left_front: <span style="color: #700000;">**KPD**</span>는 WTB 계획에 반대하며, 이를 진정한 <span style="color: #9b0000;">사회주의</span>를 향한 단계가 아닌 자본주의를 구제하는 행위로 간주합니다. ?]

@implement_wtb_deficit
title: WTB 계획을 실행한다.
subtitle: 예산 -[+ wtb_budget +] - 재정 적자가 발생할 것입니다. [? if wtb_budget == 3 : 우리의 친노동 정책 덕분에 계획 비용이 절감되었습니다. ?]
unavailable-subtitle: [? if unemployed < 8: 실업률이 낮아 WTB 계획을 시행할 시기가 지났습니다.?] [? if in_grand_coalition and (finance_minister_party == "DVP" or economic_minister_party == "DVP") and not lvp_formed: <span style="color: #C0A054;">**DVP**</span>는 [? if finance_minister_party == "DVP": 재무부?][? if economic_minister_party == "DVP" and finance_minister_party == "DVP":&nbsp;및?][? if economic_minister_party == "DVP": 경제부?] 내의 지위를 이용해 우리를 방해할 것입니다. ?][? if (in_grand_coalition or in_weimar_coalition) and (finance_minister_party == "LVP" or economic_minister_party == "LVP") and lvp_formed: <span style="color: #FFCC00;">**LVP**</span>는 [? if finance_minister_party == "LVP": 재무부?][? if economic_minister_party == "LVP" and finance_minister_party == "LVP":&nbsp;및?][? if economic_minister_party == "LVP": 경제부?] 내의 지위를 이용해 우리를 방해할 것입니다. ?] [? if in_grand_coalition and dvp_ideology != "Left" and not lvp_formed: <span style="color: #C0A054;">**DVP**</span>는 선제적으로 불신임 투표를 소집할 것입니다. ?][? if (in_grand_coalition or in_weimar_coalition) and lvp_ideology != "Left" and lvp_formed: <span style="color: #FFCC00;">**LVP**</span>는 선제적으로 불신임 투표를 소집할 것입니다. ?]
view-if: budget < wtb_budget and wtb_implemented == 0 and wtb_adopted == 1
choose-if: unemployed > 8 and (((economic_minister_party != "DVP" and finance_minister_party != "DVP" and (dvp_ideology == "Left" and in_grand_coalition) or not in_grand_coalition) and not lvp_formed) or ((economic_minister_party != "LVP" and finance_minister_party != "LVP" and (lvp_ideology == "Left" and (in_grand_coalition or in_weimar_coalition)) or (not in_grand_coalition and not in_weimar_coalition)) and lvp_formed))
on-arrival: coalition_dissent += 2; coalition_dissent += 1 if in_minority_government; coalition_dissent += 1 if chancellor == "Brüning"; coalition_dissent -= 1 if z_leader == "Kaiser"; works_program += 1; budget -= wtb_budget; unemployed -= 4; workers += 2; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); workers_spd -= 4*(1-dissent) if schleicher_spd; unemployed_spd -= 4*(1-dissent) if schleicher_spd; reichswehr_strength += 20 if schleicher_spd; reichswehr_loyalty += 0.05 if reichswehr_spd; reichswehr_militancy += 0.5 if reichswehr_spd; capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front; inflation += 3; dvp_right += 3; ddp_right += 1; ddp_cohesion -= 1; dvp_relation -= 10; ddp_relation -= 5; z_relation -= 5; lvp_relation -= 8; lvp_right += 2; pro_republic += 3*(1-dissent); economic_growth += 3; economic_growth -= 1 if schleicher_spd; economic_plan_goal_spd += 1; wtb_rally = 0
achievement: wirtschaftspolitik

우리는 적자 지출의 영역에 들어섰습니다. 연정 파트너들은 혐오감을 드러내지만, 사업 계획은 어쨌든 제정되었습니다. 대규모 공공 사업 투자를 통해 경제에 자금이 돌기 시작했습니다.

[? if in_popular_front or in_left_front: <span style="color: #700000;">**KPD**</span>는 WTB 계획에 반대하며, 이를 진정한 <span style="color: #9b0000;">사회주의</span>를 향한 단계가 아닌 자본주의를 구제하는 행위로 간주합니다. ?]


####################### left economic policies (socializations)

@nationalize_1
title: 경제 변혁을 위한 좌파 계획을 실행한다.
subtitle: 예산 -[+ nationalize_budget +]. [? if works_councils >= 3 : 직장 평의회 덕분에 프로그램에 필요한 예산이 절감되었습니다. ?] [? if socializations >= 2: 기존의 사회화 정책 덕분에 프로그램에 필요한 예산이 절감되었습니다. ?]
unavailable-subtitle: 연정 합의문에 사회화 반대 조항이 있습니다.
choose-if: not cvp_economy_accepted
view-if: nationalization_adopted == 1 and nationalization_progress == 0 and not schleicher_spd
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget


@nationalize_no_deficit
on-arrival: nationalization_progress += 1; capital_strike_progress += 2; unemployed -= 3.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; workers += 2 if nationalization_progress < 2; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 1; dvp_relation -= 4; lvp_relation -= 4; left_dissent -= 5; new_middle_spd -= 2; old_middle_spd -= 3; economy_goal_completed += 1; economic_plan_goal_spd += 1; inflation += 2; dvp_right += 2; ddp_relation -= 4; ddp_right += 3; ddp_cohesion -= 2; lvp_right += 2; goal_spd_cancel_peoples += 1; economic_growth += 1.7 if economic_growth < 5;
achievement: wirtschaftspolitik

[? if nationalization_progress == 1 : 우리는 자본가들로부터 가동 중단된 기업들을 매입하기 시작했고[? if works_councils >= 1: , 권한이 강화된 직장 평의회와 협력하여 이를 운영하기 시작했습니다 ?]. 생산을 지속함으로써 실업률 증가를 막을 수 있습니다. ?]

[? if nationalization_progress == 1 : 좌파는 이것이 경제의 <span style="color: #c00000;">사회주의적</span> 변혁을 위한 첫걸음에 불과해야 한다고 주장합니다. 결국 경제의 많은 부분이 대중의 통제하에 놓여야 합니다. ?]

[? if nationalization_progress > 1 : 우리는 가동되지 않는 기업들을 추가로 매입하여 직장 평의회와 조율된 <span style="color: #c00000;">사회주의적</span> 방식으로 운영하며, 이전에 해고된 노동자들을 고용하고 있습니다. 경제의 상당 부분이 이제 공공의 통제하에 있습니다. ?]

@nationalize_deficit

불행히도 산업 시설을 매입할 예산이 부족합니다. 일각에서는 공장 매입을 위해 적자 지출을 감수해야 한다고 주장하는 반면, 다른 이들은 (현재로서는 불법인) 노동자에 의한 공장 점거 또는 무상 국유화를 장려하고 있습니다. 이 중 어떤 조치를 취하든 기존의 당론에서 크게 벗어나야 합니다.

- @nationalize_deficit_1
- @nationalize_without_compensation
- @empower_workers
- @return: 아니, 이래선 안 된다.


@nationalize_deficit_1
title: 국유화 프로그램 자금 조달을 위해 예산 적자를 감수한다.
choose-if: left_strength > reformist_strength + center_strength or woytinsky_advisor == 1
subtitle: [? if woytinsky_advisor: 보이틴스키가 예산 적자를 지지하도록 당을 설득할 수 있습니다. ?]
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: 당은 아직 이 목적을 위한 예산 적자를 지지하지 않습니다. ?] [? if not woytinsky_advisor : 고문을 영입하면 이 조치를 받아들이게 하는 데 도움이 될지도 모릅니다. ?]
on-arrival: nationalization_progress += 1; capital_strike_progress += 3; unemployed -= 3 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 5*(1-dissent); coalition_dissent += 2; coalition_dissent -= 1 if z_leader == "Kaiser"; dvp_relation -= 6; lvp_relation -= 6; z_relation -= 4; new_middle_spd -= 3; old_middle_spd -= 3; economy_goal_completed += 1; economic_plan_goal_spd += 1; inflation += 3; dvp_right += 4; ddp_relation -= 6; ddp_right += 3; ddp_cohesion -= 2; lvp_right += 4; goal_spd_cancel_peoples += 1; economic_growth += 2 if economic_growth < 5;
achievement: wirtschaftspolitik

[? if woytinsky_advisor: 당내 대다수는, 심지어 좌파조차도 어떤 목적이든 예산 적자에 반대하지만, 블라디미르 보이틴스키만은 예외입니다. 그는 원래 좌파의 "변혁적" 계획을 지지하지 않았지만, 생각을 바꾸어 불황 탈출을 목적으로 하는 제한적인 경우에 한해 예산 적자를 지지하도록 당과 재무부를 설득했습니다. ?]

[? if nationalization_progress == 1 : 우리는 자본가들로부터 가동 중단된 기업들을 매입하기 시작했고[? if works_councils >= 1: , 권한이 강화된 직장 평의회와 협력하여 이를 운영하기 시작했습니다 ?]. 생산을 지속함으로써 실업률 증가를 막을 수 있습니다. ?]

[? if nationalization_progress == 1 : 좌파는 이것이 경제의 <span style="color: #c00000;">사회주의적</span> 변혁을 위한 첫걸음에 불과해야 한다고 주장합니다. 결국 경제의 많은 부분이 대중의 통제하에 놓여야 합니다. ?]

[? if nationalization_progress > 1 : 우리는 가동되지 않는 기업들을 추가로 매입하여 직장 평의회와 조율된 <span style="color: #c00000;">사회주의적</span> 방식으로 운영하며, 이전에 해고된 노동자들을 고용하고 있습니다. 경제의 상당 부분이 이제 공공의 통제하에 있습니다. ?]

@nationalize_without_compensation
title: 무상으로 국유화한다.
subtitle: 가동 중단된 공장을 접수하지 못할 이유가 없습니다. 이는 자본가들과의 대립을 유발할 가능성이 큽니다.
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: 당내 대다수는 이 준합법적 조치를 지지하지 않습니다. ?] [? if judicial_reform < 3 : 사법부가 이 조치에 반대할 것입니다. ?]
choose-if: left_strength > reformist_strength + center_strength and judicial_reform >= 3
on-arrival: nationalization_progress += 1; capital_strike_progress += 7; coup_progress += 3; unemployed -= 2.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3 and unemployed >= 3; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 6*(1-dissent); coalition_dissent += 2; coalition_dissent -= 1 if z_leader == "Kaiser"; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; lvp_relation -= 10; left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 8; economy_goal_completed += 1; economic_plan_goal_spd += 1; kpd_relation += 5*(1-dissent); inflation += 4; dvp_right += 5; ddp_right += 5; ddp_cohesion -= 2.5; lvp_right += 5; goal_spd_cancel_peoples += 1; economic_growth += 1 if economic_growth < 4;
achievement: wirtschaftspolitik

예상대로 자본가들은 우리의 무상 국유화 정책에 분노하고 있습니다. 그들은 이를 불법 몰수라고 부르며, 실제로도 그렇습니다. 그럼에도 불구하고 그들이 공장을 가동하지 않음으로써 실패했다는 점은 명백하며, 우리는 급진적인 조치를 취해야 합니다.

[? if nationalization_progress == 1 : 우리는 자본가들로부터 가동 중단된 기업들을 인수하기 시작했고[? if works_councils >= 1: , 권한이 강화된 직장 평의회와 협력하여 이를 운영하기 시작했습니다 ?]. 생산을 지속함으로써 실업률 증가를 막을 수 있습니다. ?]

[? if nationalization_progress > 1 : 우리는 가동되지 않는 기업들을 추가로 인수하여 직장 평의회와 조율된 <span style="color: #c00000;">사회주의적</span> 방식으로 운영하며, 이전에 해고된 노동자들을 고용하고 있습니다. 경제의 상당 부분이 이제 공공의 통제하에 있습니다. ?]

@empower_workers
title: 노동자들에게 공장 접수 권한을 부여한다.
subtitle: 공장 점거 합법화를 약속하고, 공장을 노동조합에 넘겨줄 수 있습니다. 이는 매우 대립적인 조치로, 자본가들뿐만 아니라 우익 무장세력의 반발을 유발할 가능성이 큽니다.
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : 당내 대다수는 이 급진적인 조치를 지지하지 않습니다. ?] [? if judicial_reform < 3 : 사법부가 이 조치에 반대할 것입니다. ?] [? if rb_militancy < 0.25 : 준군사조직의 힘을 키웠다면 가능했을지도 모릅니다. ?] [? if works_councils < 1 : 먼저 직장 평의회의 지지가 필요합니다. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 1; capital_strike_progress += 6; coup_progress += 5; unemployed -= 3 if unemployed >= 6; unemployed -= 1.5 if works_councils >= 3 and unemployed > 4; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; coalition_dissent -= 1 if z_leader == "Kaiser"; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; lvp_relation -= 10; workers_nsdap -= 5*(1-dissent); unemployed_nsdap -= 5*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 10; factory_takeovers += 1; economy_goal_completed += 1; economic_plan_goal_spd += 1; kpd_relation += 5*(1-dissent); inflation += 3; dvp_right += 10; ddp_right += 7.5; ddp_cohesion -= 3; lvp_right += 10; goal_spd_cancel_peoples += 1; economic_growth += 1 if economic_growth < 3;
achievement: wirtschaftspolitik

예상대로 자본가들은 공장 점거를 합법화하는 우리의 정책에 분노하고 있습니다. 그들은 이를 불법 몰수라고 부르며, 실제로도 그렇습니다. 그럼에도 불구하고 그들이 실패했다는 점은 명백하며, 우리는 급진적인 조치를 취해야 합니다.

<span style="color: #7A3C00;">파시스트들</span>은 점거된 공장 주변에서 점점 더 폭력적인 행동을 보이며 우리를 도둑이자 약탈자라고 비난하고 있습니다. 하지만 <span style="color: #7A3C00;">파시스트</span>를 지지하던 노동 계급 중 일부, 적어도 계급 연대감을 가진 이들은 이탈했습니다.


@nationalize_2
title: 경제 변혁을 위한 좌파 계획을 계속한다.
subtitle: 예산 -[+ nationalize_budget +]. [? if works_councils >= 3 : 직장 평의회 덕분에 프로그램에 필요한 예산이 절감되었습니다. ?] [? if socializations + nationalization_progress >= 2: 기존의 사회화 정책 덕분에 프로그램에 필요한 예산이 절감되었습니다. ?]
unavailable-subtitle: 연정 합의문에 사회화 반대 조항이 있습니다.
choose-if: not cvp_economy_accepted
view-if: nationalization_adopted == 1 and nationalization_progress >= 1
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget

#######################################
# Moderate economic plan

@moderate_plan
title: 개혁파의 일자리 창출 계획을 실행한다.
subtitle: 예산 -2.
unavailable-subtitle: 예산 -2. [? if budget < 2 : 필요한 예산이 부족합니다. ?] [? if unemployed < 8 : 실업률이 낮아 일자리 창출이 필요하지 않습니다. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 2 and unemployed >= 8
on-arrival: budget -= 2; works_program += 1; reformist_plan_time = time + 6 if time >= reformist_plan_time; reformist_plan_time += 6 if reformist_plan_time >= time; unemployed -= 3 if unemployed >= 8; unemployed -= 2 if unemployed >= 30; inflation += 1.5; workers_spd += 4*(1-dissent); unemployed_spd += 4*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 2*(1-dissent); rural_spd += 2*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; dvp_left += 2; ddp_left += 1; ddp_cohesion += 0.5; lvp_left += 2; economic_plan_goal_spd += 1; economy_goal_spd_peoples += 1; economic_growth += 1.6;
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : 일자리 창출 계획을 실행하기 시작했습니다! 자금이 독일 전역의 공공 사업 프로젝트 건설에 투입되어 실업률을 낮추고 있습니다. ?]

[? if moderate_plan_progress > 1 : 일자리 창출 계획에 대한 자금 지원을 계속하여, 독일 전역의 공공 사업 건설에 예산을 사용했습니다. ?]

[? if unemployed >= 10 : 부르주아들은 이 계획을 "최악을 피한" 차악의 대안으로 승인하며, 독일 경제를 구하기 위해 필요한 조치로 보고 있습니다. ?]

[? if unemployed < 10 : 실업률이 낮아 계획이 의도한 효과를 거두지 못하는 것으로 보입니다. ?]

[? if in_popular_front or in_left_front: <span style="color: #700000;">**KPD**</span>는 이 경제 계획에 반대하며, 이를 진정한 <span style="color: #9b0000;">사회주의</span>를 향한 단계가 아닌 자본주의를 구제하는 행위로 간주합니다. ?]

@moderate_plan_limited_budget
title: 개혁파의 일자리 창출 계획을 부분적으로, 더 낮은 비용으로 실행한다.
subtitle: 예산 -1.
unavailable-subtitle: 예산 -1. [? if budget < 1 : 필요한 예산이 부족합니다. ?] [? if unemployed < 8 : 실업률이 낮아 일자리 창출이 필요하지 않습니다. ?]
view-if: moderate_plan_adopted == 1
choose-if: budget >= 1 and unemployed >= 8
on-arrival: budget -= 1; works_program += 1; reformist_plan_time = time + 3 if time >= reformist_plan_time; reformist_plan_time += 3 if reformist_plan_time >= time; unemployed -= 1 if unemployed >= 8; unemployed -= 2 if unemployed >= 30; inflation += 1; workers_spd += 2*(1-dissent); unemployed_spd += 2*(1-dissent); new_middle_spd += 1.5*(1-dissent); old_middle_spd += 1*(1-dissent); rural_spd += 1*(1-dissent); moderate_plan_progress += 1; kpd_coalition_dissent += 1; inflation += 1 if unemployed < 10; kpd_relation -= 5 if in_popular_front or in_left_front; dvp_left += 1; ddp_left += 0.5; ddp_cohesion += 0.25; lvp_left += 1; economic_plan_goal_spd += 1; economy_goal_spd_peoples += 1; economic_growth += 0.7;
achievement: wirtschaftspolitik

[? if moderate_plan_progress == 1 : 일자리 창출 계획을 실행하기 시작했습니다! 자금이 독일 전역의 공공 사업 프로젝트 건설에 투입되어 실업률을 낮추고 있습니다. ?]

[? if moderate_plan_progress > 1 : 일자리 창출 계획에 대한 자금 지원을 계속하여, 독일 전역의 공공 사업 건설에 예산을 사용했습니다. ?]

[? if unemployed >= 10 : 부르주아들은 이 계획을 "최악을 피한" 차악의 대안으로 승인하며, 독일 경제를 구하기 위해 필요한 조치로 보고 있습니다. ?]

[? if unemployed < 10 : 실업률이 낮아 계획이 의도한 효과를 거두지 못하는 것으로 보입니다. ?]

[? if in_popular_front or in_left_front: <span style="color: #700000;">**KPD**</span>는 이 경제 계획에 반대하며, 이를 진정한 <span style="color: #9b0000;">사회주의</span>를 향한 단계가 아닌 자본주의를 구제하는 행위로 간주합니다. ?]
